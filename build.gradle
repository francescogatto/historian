// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: project.file("gradle/version/version.gradle")
apply from: project.file("gradle/metadata.gradle")

buildscript {
  ext.kotlin_version = '1.3.21'
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.2'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
    classpath 'com.novoda:bintray-release:0.5.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    classpath 'org.jacoco:org.jacoco.core:0.7.9'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    google() // here
    jcenter()
  }
}

ext {
  metadata.version = ext.versionName()
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

String shell(String command) {
  def proc = ["sh", "-c", "cd ${project.rootDir} ; ${command}"].execute()
  if (proc.waitFor() != 0) {
    throw new RuntimeException("Failed to run: ${command}\n${proc.err.text}")
  } else {
    def err = proc.err.text
    if (err) {
      System.err.println(err)
    }
  }
  return proc.in.text
}

task releng {
  doLast {
    def tag = "v" + versionName()
    println "Release engineering for ${tag}"

    def changes = shell "git status -s"
    if (changes.trim()) {
      throw new RuntimeException("There are changes not commited yet.\n${changes}")
    }

    println "> git tag ${tag}"
    shell "git tag ${tag}"
    println "> git push origin ${tag}"
    shell "git push origin ${tag}"
    shell "git push origin master"
  }
}
